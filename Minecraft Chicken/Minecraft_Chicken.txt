@name Minecraft Chicken

@outputs Debug:number
@persist Ranger:ranger LA

if(first()){
    #include "Minecraft Chicken/include/niHolo"
    streamDisable3D(1)
    function vector entity:relPos(X,Y,Z){
        return This:toWorld(vec(X*Scale,Y*Scale,Z*Scale))
    }
    function angle entity:relAng(A,B,C){
        return This:toWorld(ang(A,B,C))
    }
    
    #include "Minecraft Chicken/include/MC_Holo"
    
    function void fall(){
        Ranger = rangerOffset(1000,h("body"):pos(),vec(0,0,-1))
        if(Ranger:distance() > 15){
            if(Ranger:distance()-4 < 15){hPos("body",h("body"):pos()-vec(0,0,1))}
            else{hPos("body",h("body"):pos()-vec(0,0,4))}
            hAng("lWingAxis",h("body"):relAng(30+30*sin(curtime()*1000),0,0)),hAng("rWingAxis",h("body"):relAng(-30+30*sin((curtime()*1000)+500),0,0))
        }
        if(Ranger:distance() < 15){hAng("lWingAxis",h("body"):relAng(0,0,0)),hAng("rWingAxis",h("body"):relAng(0,0,0))}
    }
    function void headAim(Ent:entity){
        local HAA = (h("headAxis"):pos()-Ent:pos()-vec(0,0,(50*Ent:isPlayer())/(1+Ent:isCrouch()))):toAngle()
        local HAP = HAA:pitch(),HAY = HAA:yaw() HAR = HAA:roll()
        local HMP = (((h("headAxis"):angles():roll()-(HAP))-180)%360)-180
        local HMY = (((h("headAxis"):angles():yaw()-(HAY+90))-180)%360)-180
        hAng("headAxis",ang(HAR,h("headAxis"):angles():yaw()-(HMY/3),h("headAxis"):angles():roll()-(HMP/3)))
    }
    function void aim(Ent:entity){
        local BAA = (h("headAxis"):pos()-Ent:pos()-vec(0,0,(50*Ent:isPlayer())/(1+Ent:isCrouch()))):toAngle()
        local BAP = BAA:pitch(),BAY = BAA:yaw() BAR = BAA:roll()
        local BMY = (((h("body"):angles():yaw()-(BAY+90))-180)%360)-180
        if(BMY > 25){
            hAng("body",ang(0,h("body"):angles():yaw()-((BMY-25)/2),0))
        }
        if(BMY < -25){
            hAng("body",ang(0,h("body"):angles():yaw()-((BMY+25)/2),0))
        }
        headAim(Ent)
    }
    function void walkAnim(I:number){
        if(I){hAng("lLegAxis",h("body"):relAng(0,0,20*sin(curtime()*500))),hAng("rLegAxis",h("body"):relAng(0,0,20*sin((curtime()*500)+500)))}
        else{hAng("lLegAxis",h("body"):relAng(0,0,0)),hAng("rLegAxis",h("body"):relAng(0,0,0))}
    }
    function void followEnt(Ent:entity){
        if(Ent:pos():distance(h("body"):pos()) > 200){hPos("body",h("body"):pos()+h("body"):right()*-15),walkAnim(1),stoptimer("la"),LA = 0}
        if(Ent:pos():distance(h("body"):pos()) <= 200){walkAnim(0),timer("la",round(random(2000,5000)))}
    }
    function void aimRand(){
        aim(h("aim"))
        #x=rsin,  y=rcos
    }
    
    timer("sound",1000)
    timer("ra",100)
    #timer("egg",20000)
    
}

fall()
if(LA){aimRand()}
if(!LA){aim(owner())}
followEnt(owner())

if(clk("sound")){
    stoptimer("sound")
    streamStop(1)
    timer("sound",round(random(5000,30000)))
    local S = round(random(1,3))
    if(S == 1){h("body"):streamStart(1,"https://www.dropbox.com/s/1g3vvori57u3c6w/say1.mp3?dl=1",1)}
    if(S == 2){h("body"):streamStart(1,"https://www.dropbox.com/s/fionjyw23jr4hwi/say2.mp3?dl=1",1)}
    if(S == 3){h("body"):streamStart(1,"https://www.dropbox.com/s/crd4bx4i5kp2z2h/say3.mp3?dl=1",1)}
}
if(clk("la")){
    stoptimer("la")
    LA = 1
}
if(clk("ra")){
    stoptimer("ra")
    timer("ra",random(5000,15000))
    local A = random(0,360)
    hPos("aim",h("body"):pos()+vec(50*sin(A),50*cos(A),3.5))
}

interval(100)
